allprojects {
    // Maven compatibility
    apply plugin: 'maven'
    group = 'com.github.micromix'
    version = '0.3-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    ext {
        camelVersion = '2.12.3'
        camelSpring = "org.apache.camel:camel-spring:$camelVersion"
        junit = 'junit:junit:4.11'
        scalaLibrary = 'org.scala-lang:scala-library:2.10.3'
        scalaTest = 'org.scalatest:scalatest_2.10:2.1.0'
    }

    // Releasing
    apply plugin: 'scala' // Workaround - scaladocJar needs this
    apply plugin: 'signing'

    ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

    task scaladocJar(type: Jar, dependsOn: scaladoc) {
        classifier = 'javadoc'
        from 'build/docs/scaladoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives scaladocJar
        archives sourcesJar
    }

    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                def snapshotRepo = 'https://oss.sonatype.org/content/repositories/snapshots/'
                def releaseRepo = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                repository(url: isReleaseVersion ? releaseRepo : snapshotRepo) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'micromix'
                    packaging 'jar'
                    url 'https://github.com/hekonsek/micromix'
                    description 'MicroMix'

                    scm {
                        url 'scm:git@github.com:hekonsek/micromix.git'
                        connection 'scm:git@github.com:hekonsek/micromix.git'
                        developerConnection 'scm:git@github.com:hekonsek/micromix.git'
                    }

                    developers {
                        developer {
                            id 'hekonsek'
                            name 'Henryk Konsek'
                        }
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }
    }

}

project('micromix-boots') {
    project('micromix-boot-spring') {
        apply plugin: 'scala'

        dependencies {
            compile scalaLibrary
            compile 'org.springframework:spring-context:3.2.5.RELEASE'
            compile 'com.google.guava:guava:16.0.1'

            testCompile junit
        }
    }
}

project('micromix-conflets') {
    project('micromix-conflet-camel') {
        project('micromix-conflet-camel-spring') {
            apply plugin: 'scala'

            dependencies {
                compile project(':micromix-boots:micromix-boot-spring')

                compile scalaLibrary
                compile camelSpring

                testCompile junit
                testCompile scalaTest
                testCompile 'ch.qos.logback:logback-classic:1.0.13'
            }
        }
    }

    project('micromix-conflet-hibernate42') {
        project('micromix-conflet-hibernate42-spring') {
            apply plugin: 'java'

            dependencies {
                compile project(':micromix-boots:micromix-boot-spring')

                compile 'org.hibernate:hibernate-entitymanager:4.2.9.Final'
                compile 'org.springframework:spring-context:3.2.5.RELEASE'
                compile 'org.springframework:spring-orm:3.2.5.RELEASE'

                testCompile junit
                testCompile 'ch.qos.logback:logback-classic:1.0.13'
            }
        }

        project('micromix-conflet-hibernate42-springdm') {
            apply plugin: 'osgi'

            dependencies {
                compile 'org.springframework:spring-tx:3.2.5.RELEASE'
            }
        }

        project('micromix-conflet-hibernate42-springdm-itest') {
            apply plugin: 'java'

            dependencies {
                compile project(':micromix-conflets:micromix-conflet-hibernate42:micromix-conflet-hibernate42-springdm')
                compile project(':micromix-conflets:micromix-conflet-hibernate42:micromix-conflet-hibernate42-springdm-itest-business')
                compile project(':micromix-conflets:micromix-conflet-hibernate42:micromix-conflet-hibernate42-springdm-itest-route')

                testCompile junit
                testCompile 'com.google.guava:guava:14.0'
                testCompile 'org.ops4j.pax.exam:pax-exam-container-karaf:3.4.0'
                testCompile 'org.ops4j.pax.exam:pax-exam-junit4:3.4.0'
                testCompile 'org.ops4j.pax.exam:pax-exam-container-rbc:3.4.0'
                testCompile 'org.ops4j.pax.exam:pax-exam-link-mvn:3.4.0'
                testCompile 'org.ops4j.pax.url:pax-url-aether:1.6.0'
                testCompile 'org.apache.felix:org.apache.felix.framework:3.2.2'
            }
        }

        project('micromix-conflet-hibernate42-springdm-itest-business') {
            apply plugin: 'osgi'

            dependencies {
                compile "org.apache.camel:camel-core:$camelVersion"
                compile 'org.springframework:spring-orm:3.2.5.RELEASE'
                compile 'org.hibernate:hibernate-entitymanager:4.2.9.Final'
                compile 'org.apache.geronimo.specs:geronimo-jpa_2.0_spec:1.1'
                compile 'org.apache.geronimo.specs:geronimo-jta_1.1_spec:1.1.1'
            }

            jar {
                manifest {
                    instruction 'Import-Package', 'javax.persistence, javax.persistence.spi, org.hibernate.proxy, javassist.util.proxy, org.hsqldb.jdbc, org.hsqldb, *'
                    instruction 'Meta-Persistence', 'META-INF/persistence.xml'
                }
            }
        }

        project('micromix-conflet-hibernate42-springdm-itest-route') {
            apply plugin: 'osgi'

            dependencies {
                compile 'org.apache.geronimo.specs:geronimo-jpa_2.0_spec:1.1'
                compile 'org.apache.camel:camel-jpa:2.12.3'
            }

            jar {
                manifest {
                    instruction 'Import-Package', 'javax.persistence, javax.persistence.spi, org.hibernate.proxy, javassist.util.proxy, org.hsqldb.jdbc, org.hsqldb, *'
                    instruction 'Meta-Persistence', 'META-INF/persistence.xml'
                }
            }
        }

        project('micromix-conflet-hibernate42-springdm-itest-datasource') {
            apply plugin: 'osgi'

            jar {
                manifest {
                    instruction 'Import-Package', 'javax.sql, org.hibernate.proxy, javassist.util.proxy, org.hsqldb.jdbc, org.hsqldb, *'
                }
            }
        }
    }

    project('micromix-conflet-restgateway') {
        project('micromix-conflet-restgateway-spring') {
            apply plugin: 'scala'

            dependencies {
                compile scalaLibrary
                compile 'org.springframework:spring-context:3.2.5.RELEASE'
                compile 'org.apache.camel:camel-netty-http:2.12.3'
                compile camelSpring
                compile 'org.apache.camel:camel-jackson:2.12.3'

                testCompile project(':micromix-boots:micromix-boot-spring')
                testCompile project(':micromix-conflets:micromix-conflet-camel:micromix-conflet-camel-spring')

                testCompile junit
                testCompile scalaTest
                testCompile 'ch.qos.logback:logback-classic:1.0.13'
            }
        }
    }
}